buildscript {
    repositories { jcenter() }
    dependencies { classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0' }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version '1.1.4-2'
    id "org.jetbrains.kotlin.plugin.allopen" version "1.1.4-2"
    id "org.jetbrains.kotlin.plugin.noarg" version "1.1.4-2"
    id "org.jetbrains.kotlin.plugin.jpa" version "1.1.4-2"
    id "org.jetbrains.kotlin.plugin.spring" version "1.1.4-2"
    id "org.jmailen.kotlinter" version "1.2.0"
    id 'org.springframework.boot' version "1.5.6.RELEASE"
    id "java"
}

apply plugin: 'org.junit.platform.gradle.plugin'

ext {
    kotlinVersion = '1.1.4-2'
    springBootVersion = '1.5.6.RELEASE'
    caveVersion = '0.0.1-SNAPSHOT'
    junitJupiterVersion = '5.0.0'
    junitPlatformVersion = '1.0.0'
}

group 'com.osori'
version "${caveVersion}"


sourceCompatibility = 1.8

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: springBootVersion
    compile group: 'org.springframework.hateoas', name: 'spring-hateoas', version: '0.23.0.RELEASE'

    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8', version: kotlinVersion
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlinVersion

    compile group: 'com.github.kittinunf.fuel', name: 'fuel', version: '1.9.0'
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.9.2'

    compile group: 'au.com.console',name: 'kotlin-jpa-specification-dsl', version: '0.1.0' //https://github.com/consoleau/kotlin-jpa-specification-dsl

    compile group: 'org.hibernate', name: 'hibernate-envers'
    compile group: 'org.hibernate', name: 'hibernate-java8'

    compile files('libs/spring-trace-core-1.0.2.jar')
    compile files('libs/spring-trace-web-1.0.2.jar')

    runtime group: 'com.h2database', name: 'h2', version: '1.4.196'

    testCompile group: 'com.nhaarman', name: 'mockito-kotlin', version: '1.5.0'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
    testCompile group: 'org.junit.platform', name: 'junit-platform-launcher', version: junitPlatformVersion
    testCompile group: 'com.github.sbrannen', name: 'spring-test-junit5', version: '1.0.0'
    testCompile group: 'io.kotlintest', name: 'kotlintest', version: '2.0.5'

}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

bootRun {
    systemProperties['spring.profiles.active'] = project.gradle.startParameter.systemPropertiesArgs['spring.profiles.active']
    addResources = true
}

jar {
    baseName = 'osori-cave'
    version = "${caveVersion}"
    //만들어지는 결과물 osori-cave-0.0.1-SNAPSHOT.jar
}


sourceSets {
    main {
        java {
            srcDir 'src/main/kotlin'
        }
    }
}

compileKotlin.dependsOn formatKotlin

task caveVersion << {
    println "${caveVersion}"
}
